//file:noinspection GroovyAssignabilityCheck
plugins {
    id "java"
    id "maven-publish"
    id "signing"
}

def projectVersion = properties["projectVersion"] ?: "1.0.SNAPSHOT"
def projectGroup = properties["projectGroup"] ?: "com.vladsch.unknown"
def projectModules = properties["projectModules"] ?: "com.vladsch.unknown"
def projectUrl = properties["projectUrl"] ?: "https://github.com/vsch/unknown"

group = projectGroup
version = projectVersion

dependencies {
    projectModules.split(",").each {
        module "${projectGroup}:${it}:${projectVersion}"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "reverse-regex"
                description = "Java Implementation of RegEx Pattern Reversing library for backwards searching using Java Pattern class and a reversed character sequence of the original text"
                url = projectUrl

                licenses {
                    license {
                        name = "GNU Lesser General Public License version 2.1"
                        url = "https://opensource.org/licenses/LGPL-2.1"
                    }
                }

                developers {
                    developer {
                        id = "vsch"
                        name = "Vladimir Schneider"
                        email = "vladimir.schneider@gmail.com"
                        organizationUrl = "https://github.com/vsch/"
                    }
                }

                scm {
                    connection = "scm:git:git@github.com:vsch/reverse-regex.git"
                    developerConnection = "scm:git:git@github.com:vsch/reverse-regex.git"
                    url = projectUrl
                    tag = projectVersion
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
/*
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/vsch/reverse-regex"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
*/
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}

